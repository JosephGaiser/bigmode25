if {Chet.day_1} == true:
	chet: we can talk later.
	[end_timeline]
set {Chet.day_1} = true
chet: .. so, I know you Uhh..
chet: ...
[wait time="0.5"]
chet: Uhh.. Would you maybe...
chet: Well... It would be an honor if you visited my church for service... with me.
[wait time=".5"]
chet: or.. just I would see you there. I guess.
- Say you will go, but don't.
	set {Chet.day_1_decision} = "lie"
	[wait time="1.0"]
	chet: Really?
	chet: ...
	do power_manager.lose_authority(5.0)
	do power_manager.gain_moral(5.0)
- Respectfully decline.
	set {Chet.day_1_decision} = "decline"
	chet: I understand, just know we are always happy to have you when you change your mind.
	chet: I mean if.... Ha
	[wait time="1.0"]
	chet: Do you want to hear a scripture?
	- Listen
		chet: hold on, let me find it...  Its dogeared in my copy of the holy book
		[signal arg="read"]
		chet: wait... I'll tell you later.
		do power_manager.lose_moral(5.0)
		do power_manager.gain_authority(5.0)
	- Refuse
		chet: I understand, just know I am always happy to tell you when you change your mind.
		do power_manager.lose_authority(5.0)
		do power_manager.gain_moral(5.0)
- Lie that you already have a church.
	set {Chet.day_1_decision} = "church"
	[text_input var="church_name"]
	chet: Oh... {church_name}...
	[wait time="0.3"]
	chet: Just maybe don't bring {church_name} up at the office...
	chet: Remember; that training we had.
	do power_manager.lose_reputation(5.0)
	do power_manager.gain_authority(5.0)
- Offer for Carl to go in your place
	set {Chet.day_1_decision} = "carl"
	chet: He did just lose his family...
	chet: That will be so great for him, he is going to be so grateful.
	[wait time="0.4"]
	chet: You are doing a good thing, Boss.
	do power_manager.lose_moral(5.0)
	do power_manager.gain_reputation(5.0)
